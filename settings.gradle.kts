pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven("https://maven.fabricmc.net/") { name = "FabricMC" }
        maven("https://maven.neoforged.net/releases/") { name = "NeoForged" }
        maven("https://maven.kikugie.dev/releases") { name = "KikuGie Releases" }
        maven("https://maven.kikugie.dev/snapshots") { name = "KikuGie Snapshots" }
    }
}

plugins {
    // Check the latest version on https://stonecutter.kikugie.dev/blog/changes/0.9
    id("dev.kikugie.stonecutter") version "0.9.7"

    // Used for cross-compat for 26.1+ and older versions (https://codeberg.org/KikuGie/loom-back-compat)
    id("dev.kikugie.loom-back-compat") version "0.4.2"

    // Sometimes it is needed to make Gradle run at all, so it doesn't hurt to have
    // (https://github.com/gradle/foojay-toolchains)
    id("org.gradle.toolchains.foojay-resolver-convention") version "1.0.0"
}

stonecutter {
    create(rootProject) {
        /**
         * Creates version nodes for multiple loaders.
         *
         * This function will create subprojects named `versions/{project}-{loader}`.
         * Each project has a logical [version], which should match the Minecraft version,
         * whereas [project] is the arbitrary name part of the folder.
         *
         * Each project will also have a separate build script assigned depending on the loader,
         * named `build.{loader}.gradle.kts`.
         */
        fun match(project: String, vararg loaders: String, version: String = project) {
            for (loader in loaders) version("$project-$loader", version).buildscript("build.$loader.gradle.kts")
        }

        // 24w09a (1.20.5) moved getWeight from BundleItem to BundleContents
        // 24w14a (1.20.5) changed getWeight output from int to Fraction
        // 26.1-snapshot-3 changed getWeight output from Fraction to DataResult<Fraction> and input from ItemStack to ItemInstance

        // See https://stonecutter.kikugie.dev/wiki/start/#choosing-minecraft-versions
        match("1.17_1.17.1", "fabric", version = "1.17")
        match("1.18_1.20.4", "fabric", version = "1.18") // Because 1.18 requires java 17
        match("1.20.5_1.20.6", "fabric", version = "1.20.5")
        match("1.21_1.21.11", "fabric", "neoforge", version = "1.21") // When neo appeared (well when it started gaining traction)
        match("26.1_2", "fabric", "neoforge", version = "26.1")
        vcsVersion = "26.1_2-fabric"
    }
}

rootProject.name = "Bundle Tweak"
